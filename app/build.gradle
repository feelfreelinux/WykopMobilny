apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

kapt {
    correctErrorTypes = true
}

androidExtensions {
    experimental = true
}

static def getBugsnagKey() {
    return System.getenv('BUGSNAG_API_KEY') ? System.getenv('BUGSNAG_API_KEY') : ''
}

static def isWeeklyBuild() {
    return System.getenv('TRAVIS_EVENT_TYPE') == "cron" ? "true" : "false"
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "io.github.feelfreelinux.wykopmobilny"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode project.ext.versionMajor * 10000 + project.ext.versionMinor * 1000 + project.ext.versionPatch * 100 + project.ext.versionBuild
        versionName "${project.ext.versionMajor}.${project.ext.versionMinor}.${project.ext.versionPatch}.${project.ext.versionBuild}"
        multiDexEnabled true

        def credentialsPropertiesFile = rootProject.file("credentials.properties")
        def credentialsProperties = new Properties()
        credentialsProperties.load(new FileInputStream(credentialsPropertiesFile))

        buildConfigField "String", "IS_WEEKLY", "\"${isWeeklyBuild()}\""
        buildConfigField "String", "BUGSNAG_KEY", "\"${getBugsnagKey()}\""
        buildConfigField 'String', 'APP_SECRET', credentialsProperties['apiSecret']
        buildConfigField 'String', 'APP_KEY', credentialsProperties['apiKey']
        buildConfigField 'String', 'GOOGLE_KEY', credentialsProperties['googleKey']
        resValue 'string', 'GOOGLE_API_KEY', credentialsProperties['googleKey']
    }
    
    signingConfigs {
        ci {
            keyAlias "key0"
            keyPassword System.getenv('CI_KEYSTORE_PASSWORD')
            storeFile file('../ci-release-keystore.jks')
            storePassword System.getenv('CI_KEYSTORE_PASSWORD')
        }
    }
    
    buildTypes {
        release {
            if(System.getenv('CI_KEYSTORE_PASSWORD')) signingConfig signingConfigs.ci
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Support library
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "android.arch.work:work-runtime:1.0.1"
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.devbrackets.android:exomedia:4.3.0'
    def exoplayerVersion = "2.9.6"
    implementation "com.google.android.exoplayer:exoplayer:$exoplayerVersion"
    implementation "com.google.android.exoplayer:extension-okhttp:$exoplayerVersion"
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.core:core-ktx:1.5.0'

    // Utils
    implementation 'com.bugsnag:bugsnag-android:4.5.0'
    implementation "org.ocpsoft.prettytime:prettytime:4.0.4.Final"
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.pavelsikun:material-seekbar-preference:2.3.0'
    implementation 'com.github.feelfreelinux:Slidr:2.2.1'

    // Retrofit
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"
    implementation 'pl.droidsonroids:jspoon:1.3.0'
    implementation 'pl.droidsonroids.retrofit2:converter-jspoon:1.3.0'

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxjava:2.1.3"
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation 'org.jetbrains:markdown:0.1.28'


    // AppUpdater
    implementation "com.github.javiersantos:AppUpdater:2.6.3"

    // Dagger 2
    def daggerVersion = "2.36"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    // Glide
    def glide = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide"
    kapt "com.github.bumptech.glide:compiler:$glide"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide"

    // Android-Job
    implementation "com.evernote:android-job:1.2.6"
    implementation 'com.github.suckgamony.RapidDecoder:library:7cdfca47fa'

    // Unit tests
    testImplementation "org.mockito:mockito-core:3.9.0"
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha3') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
}
